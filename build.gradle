plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.ec4j.editorconfig' version "0.1.0"
    id 'checkstyle'
    id "org.sonarqube" version "6.3.1.5724"
    id 'jacoco'
}

group = 'Dalcoomi'
version = '0.0.1-SNAPSHOT'
jar.enabled = false

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

configurations {
    configureEach {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }

    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

checkstyle {
    toolVersion = "10.4"
    maxWarnings = 0
    configFile = file("${rootDir}/config/naver-checkstyle-rules.xml")
    configProperties = ["suppressionFile": "${rootDir}/config/naver-checkstyle-suppressions.xml"]
}

check.dependsOn editorconfigCheck

editorconfig {
    excludes = ['build']
}

sonar {
    properties {
        property "sonar.projectKey", "Dalcoomi_Server"
        property "sonar.organization", "dalcoomi"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.exclusions", "**/*Application*.java, **/*Config*.java, **/*GlobalExceptionHandler.java, " +
                "**/Q*.java, **/DynamicQuery.java, **/*Exception.java, **/*Adapter.java, **/*Filter.java, " +
                "**/BaseTimeEntity.java, **/*Converter.java, **/*RepositoryImpl.java, **/HealthController.java" +
                "**/*Warmup.java, **/Dummy*.java"
        property "sonar.java.coveragePlugin", "jacoco"
    }
}

jacoco {
    toolVersion = "0.8.12"
}

test {
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        html.required = true
    }

    def Qdomains = []

    for (qPattern in '**/QA'..'**/QZ') { // qPattern = '**/QA', '**/QB', ... '*.QZ'
        Qdomains.add(qPattern + '*')
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, excludes: [
                            "**/*Application*",
                            "**/*Config*",
                            "**/*DynamicQuery*",
                            "**/*error*",
                            "**/*Repository*"
                    ] + Qdomains)
                })
        )
    }

    finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = 'CLASS'

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }

            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }

            excludes = [
                    '*.*Application',
                    '*.*Config',
                    '*.error.*',
                    '*.dto.*',
                    '*.Q*',
                    '*.DynamicQuery',
                    '*.*Adapter',
                    'scouter.*',
                    'reactor.*',
                    '*.*RepositoryImpl',
                    '*.BaseTimeEntity',
                    '*.*Converter',
                    '*.*.CustomUserDetails',
                    '*.HealthController',
                    'com.dalcoomi.transaction.application.ReceiptStreamConsumer.*',
                    '*.*Warmup',
                    '*.Dummy*',
                    '*.DecryptionMigrationBatchService',
                    '*.DecryptionMigrationController',
                    '*.EncryptionService',
                    '*.ReceiptSseService',
                    '*.ReceiptPubSubPublisher'
            ]
        }
    }
}

ext {
    queryDslVersion = "6.11"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    // Lombok
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    // TSID
    implementation 'io.hypersistence:hypersistence-utils-hibernate-63:3.9.0'

    // Log4j2
    // -DLog4jContextSelector=org.apache.logging.log4j.core.async.AsyncLoggerContextSelector -> VM for Async Logger
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.18.2'
    implementation 'com.lmax:disruptor:4.0.0'

    // H2
    runtimeOnly 'com.h2database:h2'

    // MySQL
    runtimeOnly 'com.mysql:mysql-connector-j'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Flyway
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'

    // Querydsl
    implementation("io.github.openfeign.querydsl:querydsl-core:$queryDslVersion")
    implementation("io.github.openfeign.querydsl:querydsl-jpa:$queryDslVersion")
    annotationProcessor("io.github.openfeign.querydsl:querydsl-apt:$queryDslVersion:jpa")
    annotationProcessor("jakarta.annotation:jakarta.annotation-api")
    annotationProcessor("jakarta.persistence:jakarta.persistence-api")

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'io.projectreactor:reactor-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Test Container
    testImplementation "org.testcontainers:testcontainers:2.0.2"
    testImplementation "org.testcontainers:junit-jupiter:1.20.4"

    // AWS S3 SDK
    implementation 'software.amazon.awssdk:s3:2.32.29'

    // Thumbnailator - 이미지 리사이징과 포맷 변환
    implementation 'net.coobird:thumbnailator:0.4.20'

    // 이미지 메타데이터 관련 라이브러리
    implementation 'com.drewnoakes:metadata-extractor:2.19.0'
    implementation 'org.apache.commons:commons-imaging:1.0.0-alpha6'
}
